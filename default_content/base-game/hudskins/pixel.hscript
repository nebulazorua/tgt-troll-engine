introSnds = ["intro3-pixel","intro2-pixel","intro1-pixel","introGo-pixel"];
introAlts = ["pixelUI/onyourmarks-pixel", 'pixelUI/ready-pixel', 'pixelUI/set-pixel', 'pixelUI/date-pixel'];

var RatingSprite = getClass("RatingSprite"); // hmmmmm
var ratingGroup;

//var pixelJudges:Array<String> = ['miss', 'epic', 'sick','good','bad','shit'];

var lastCombos = game.lastCombos;
var lastJudge = null;

rosesHealthBar = true;

var hud;
var hb;
var bg;

var timerCirc;
var timerNums = [];
var timerSpecial;

var roundTo = CoolUtil.snap;

function onCreatePost()
{
	ratingGroup = state.ratingGroup;
	if (RatingSprite == null)
		RatingSprite = Type.getClass(ratingGroup.members[0]); // ridiculous LOL
	
	hud = game.hud;
	
	hb = hud.healthBar;
	bg = hud.healthBarBG;
	
	////
	hud.timeTxt.visible = false;
	hud.timeBar.visible = false;
	hud.timeBarBG.visible = false;
	hud.updateTime = false;
	
	var graphic = Paths.image("pixelUI/timer-circ");
	timerCirc = new FlxSprite((FlxG.width - graphic.width * 6) / 2, 0, graphic);
	timerCirc.antialiasing = false;
	timerCirc.scale.set(6, 6);
	timerCirc.updateHitbox();
	timerCirc.offset.y += timerCirc.height / 2;
	timerCirc.alpha = 0.0;
	hud.insert(hud.members.indexOf(hud.timeBarBG), timerCirc);
	
	timerSpecial = new FlxSprite();
	timerSpecial.scale.set(6, 6);
	timerSpecial.antialiasing = false;
	timerSpecial.exists = false;
	timerSpecial.alpha = 0.0;
	hud.insert(hud.members.indexOf(timerCirc) + 1, timerSpecial);
	
	adjustClock();
	////
	
	if (rosesHealthBar==true && hb != null && bg != null)
	{
		hud.remove(bg);
		hud.insert(hud.members.indexOf(hb) + 1, bg);
		
		bg.color = 0xFFFFFFFF;
		bg.loadGraphic(Paths.image("pixelUI/healthBar"));
		bg.scale.set(6, 6);
		bg.updateHitbox();
		
		adjustHealthBar();
	
	}else{
		rosesHealthBar = false;
	}
}

function adjustHealthBar()
{
	bg.offset.set(
		-0.5 * (bg.width - bg.frameWidth) - 5, 
		-0.5 * (bg.height - bg.frameHeight) - 5
	);
	
	var offY = bg.height / 2 - 5;
	
	bg.offset.y += offY;
	bg.offset.x += (bg.width - hb.width) / 2;
	
	hb.y = (ClientPrefs.downScroll ? 0 : FlxG.height - bg.height) + offY;
	
	hud.iconP1.y = hb.y + (hb.height - hud.iconP1.height) / 2;
	hud.iconP2.y = hb.y + (hb.height - hud.iconP2.height) / 2;
}

function adjustClock()
{
	timerCirc.visible = (ClientPrefs.timeBarType != "Disabled");
	setTimerTxt("");
	
	timerCirc.y = Note.swagWidth / 2 + 45;
	if (ClientPrefs.downScroll) timerCirc.y = FlxG.height - timerCirc.y;
	
	for (spr in timerNums){
		spr.y = timerCirc.y	- timerCirc.height / 2 + 11*6;
	}
	
	if (ClientPrefs.timeBarType == "Song Name"){
		timerSpecial.loadGraphic(Paths.image("pixelUI/clock-texts/"+game.songName));
		timerSpecial.updateHitbox();
		timerSpecial.exists = true;
		timerSpecial.setPosition(
			(FlxG.width - timerSpecial.width) / 2,
			timerCirc.y - timerSpecial.height / 2
		);
	}else{
		timerSpecial.exists = false;
	}
		
	if (hud.botplayTxt != null)
		hud.botplayTxt.y = timerCirc.y - hud.botplayTxt.height / 2;
}

function optionsChanged(changed:Array)
{
	if (rosesHealthBar && changed.contains("downScroll")){
		adjustHealthBar();
		bg.setPosition(hb.x - 5, hb.y - 5); // game is probably paused so it wont update
	}
	
	adjustClock();
}

function onCountdownTick(tick)
{
	//trace("tick" + tick);
	if (tick==3){
		FlxTween.tween(timerCirc, {alpha: 1}, Conductor.crochet / 1000);
	}
	else if (tick == 4)
	{
		updateTimerTxt();

		timerSpecial.alpha = 0.0;
		FlxTween.tween(timerSpecial, {alpha: 1}, Conductor.crochet / 1000);
		
		for (spr in timerNums){
			spr.alpha = 0.0;
			FlxTween.tween(spr, {alpha: 1}, Conductor.crochet / 1000);
		}
	}
}
function onSongStart()
{
	timerCirc.alpha = 1.0;
	timerSpecial.alpha = 1.0;
}

function formatTime(time:Float)
{
	var displayMinutes = Std.string(Std.int(time / 60));
	var displaySeconds = Std.string(Std.int(time % 60));
	while (displaySeconds.length < 2)
		displaySeconds = "0" + displaySeconds;
	
	return displayMinutes + ":" + displaySeconds;
}

var curClockTxt = null;
function updateTimerTxt()
{
	var curTime:Float = Math.max(0, Conductor.songPosition - ClientPrefs.noteOffset);
	
	timerSpecial.exists = false;
	
	var txt:String = "";
	switch (ClientPrefs.timeBarType)
	{
		case "Percentage":
			txt = Math.floor((curTime / game.songLength) * 100) + "%";
			
		case "Time Left":
			txt = (formatTime((game.songLength - curTime) / 1000 / FlxG.timeScale));
			
		case "Time Elapsed":
			txt = (formatTime(curTime / 1000 / FlxG.timeScale));		
			
		case "Song Name":
			txt = "";
			timerSpecial.exists = true;
	}
	
	if (curClockTxt != txt)
		setTimerTxt(txt);
}

function onUpdate(elapsed){	
	if (!(game.startingSong || game.endingSong))
		game.cpuControlled ? setTimerTxt("") : updateTimerTxt();
}


function setTimerTxt(text:String)
{
	curClockTxt = text;

	var splitText = text.split("");
	var totalWidth = 5*6*splitText.length - 6;
	var offset = 0;
	
	if (splitText.contains(":")){
		totalWidth-=3*6;
	}
	
	for (spr in timerNums){
		spr.exists = false;
	}
	
	for (i in 0...splitText.length){
		var char = splitText[i];
		
		var spr = timerNums[i];
		if (spr==null) spr = makeTimerSpr(char);
		
		spr.animation.play(char);
		spr.exists = true;
		
		spr.x = timerCirc.x + timerCirc.width / 2 - totalWidth / 2 + i * 5*6 + offset;
		
		if (char==":"){
			spr.x -= 6;
			offset -= 3*6;
		}
	}
}

var timerNumsGraphic = Paths.image("pixelUI/timer-nums");
function makeTimerSpr(?name)
{
	var spr = new FlxSprite().loadGraphic(timerNumsGraphic, true, 5, 6);
	spr.y = timerCirc.y	- timerCirc.height / 2 + 11*6;

	for (i in 0...10)
	spr.animation.add(""+i, [i], 0, false);
	spr.animation.add("%", [10], 0, false);
	spr.animation.add(":", [11], 0, false);
	spr.animation.play((name != null) ? name : "0");
	
	spr.antialiasing = false;
	spr.scale.set(6,6);
	spr.updateHitbox();
	
	timerNums.push(spr);
	hud.insert(hud.members.indexOf(timerCirc)+1, spr);
	
	return spr;
}

function prepareRatingSpr()
{
	var spr = new RatingSprite();
	spr.moves = false;
	return spr;
}
function getRatingSpr(name, ?spr){
	var spr = spr!=null ? spr : ratingGroup.recycle(RatingSprite, prepareRatingSpr, false, true);
	
	spr.antialiasing = false;
	spr.loadGraphic(Paths.image("pixelUI/"+name+"-pixel"));
	spr.scale.set(6.0, 6.0);
	spr.updateHitbox();

	return spr;
}

function anim(spr, startX, targetX, startY, targetY, prog){
	if (prog < 0.5)
		spr.alpha = FlxEase.backOut(prog *2);
	else if (prog > 0.8)
		spr.alpha = 5 - 5 * prog;

	var num = FlxEase.backOut(prog > 0.5 ? 1 : prog*2);
	spr.offset.set(
		roundTo( FlxMath.lerp(startX, targetX, FlxEase.circIn(prog)), 6 ),
		FlxMath.lerp(startY, targetY, num)
	);
}

function onDisplayCombo(combo)
{
	if (combo == 0 || (!ClientPrefs.simpleJudge && combo > 0 && combo < 10))
		return Function_Stop;

	var comboColor = game.comboColor;
	var splitCombo = Std.string(Math.abs(combo)).split();
	while (splitCombo.length < 3)
		splitCombo.unshift("0");
	if (combo < 0){
		splitCombo.unshift("neg");		
		comboColor = 0xFFF7C5DC;
	}
		
	var bs = 0;
	var totalWidth = 9 * 6 * splitCombo.length;
	
	var posX = roundTo(game.ratingOrigin[0] + ClientPrefs.comboOffset[2], 6);
	var posY = roundTo(game.ratingOrigin[1] - ClientPrefs.comboOffset[3], 6);
	
	if (ClientPrefs.simpleJudge){
		while (lastCombos.length > 0)
			lastCombos.pop().kill();
	}
	
	for (char in splitCombo){	
		var spr = getRatingSpr("num"+char);
		spr.color = comboColor;

		spr.setPosition(posX, posY);
		spr.offset.set(
			totalWidth / 2 - (9 * 6 * bs),
			spr.height / 2
		);
		
		if (ClientPrefs.simpleJudge){
			
		}else{
			var startX	=	spr.offset.x;
			var targetX =	spr.offset.x - 6*9;
			var startY	=	spr.offset.y - spr.height;
			var targetY =	spr.offset.y;
			spr.tween = FlxTween.num(
				0, 1, 1.2, 
				{onComplete: spr.kill}, 
				(prog)->{anim(spr, startX, targetX, startY, targetY, prog);}
			);
		}
		
		lastCombos.push(spr);
		ratingGroup.remove(spr, true);
		ratingGroup.add(spr);
		
		bs++;
	}

	return Function_Stop;
}

function onDisplayJudgment(name)
{
	if (ClientPrefs.simpleJudge && lastJudge != null){
		lastJudge.kill();
	}
	
	var spr = getRatingSpr(name);
	spr.color = 0xFFFFFFFF;
	
	spr.setPosition(
		roundTo(game.ratingOrigin[0] + ClientPrefs.comboOffset[0], 6), 
		roundTo(game.ratingOrigin[1] - ClientPrefs.comboOffset[1], 6)
	);
	spr.offset.set(
		spr.width / 2, 
		spr.height / 2
	);
	
	//trace(spr, spr.alpha, spr.scale);
	
	if (ClientPrefs.simpleJudge){
		var startScale = spr.scale.x;
		var time = (Conductor.stepCrochet * 0.001);
		
		var playNextTwn = ()->{
			spr.tween = FlxTween.num(
				startScale, 0, time, 
				{
					startDelay: time * 8,
					ease: FlxEase.quadIn,
					onComplete: spr.kill
				},
				(val)->{spr.scale.set(val, val);}
			);
		}
		
		spr.tween = FlxTween.num(
			1.0, 0.0, 0.1, 
			{ease: FlxEase.quadOut, onComplete: playNextTwn},
			(val)->{spr.scale.set(startScale + val, startScale + val);}
			
		);
		
	}else{
		var startX	=	spr.offset.x;
		var targetX =	spr.offset.x + 6*9;
		var startY	=	spr.offset.y - spr.height;
		var targetY =	spr.offset.y;
		
		anim(spr, startX, targetX, startY, targetY, 0.0);
		spr.tween = FlxTween.num(
			0.0, 1.0, 1.2, 
			{onComplete: spr.kill}, 
			(prog)->{anim(spr, startX, targetX, startY, targetY, prog);}
		);
	}
	
	lastJudge = spr;
	ratingGroup.remove(spr, true);
	ratingGroup.add(spr);

	return Function_Stop;
}

function makeCountdownSprite(sprImage, swagCounter, tmr){
	var countdownSpr = new FlxSprite(0, 0, sprImage);
	countdownSpr.scrollFactor.set();
	countdownSpr.scale.set(6, 6);
	countdownSpr.updateHitbox();
	countdownSpr.cameras = [state.camHUD];
	countdownSpr.screenCenter();
	countdownSpr.antialiasing = false;
	return countdownSpr;
}

function setupNoteTexture(note:Note){
	if(note.isSustainNote)
		note.reloadNote(note.texPrefix, 'NOTE_assets', note.texSuffix + 'ENDS', 'pixelUI', 4, 2);
	else
		note.reloadNote(note.texPrefix, 'NOTE_assets', note.texSuffix, 'pixelUI', 4, 5);

	note.scale.set(6, 6);
	note.antialiasing = false;
}

function setupReceptorTexture(strum:StrumNote){
	strum.isQuant = false;
	
	var lastAnim:String = null;
	if (strum.animation.curAnim != null) 
		lastAnim = strum.animation.curAnim.name;
	
	var br:String = 'NOTE_assets';
	if (PlayState.SONG.arrowSkin != null && PlayState.SONG.arrowSkin.length > 1)
		br = PlayState.SONG.arrowSkin;
	
	if (ClientPrefs.noteSkin == 'Quants')
	{
		if (Paths.exists(Paths.getPath("images/pixelUI/QUANT" + br + ".png", "IMAGE")) || Paths.exists(Paths.modsImages("pixelUI/QUANT" + br))) {
			br = "QUANT" + br;
			strum.isQuant = true;
		}
	}
	
	// TODO: add an easy way to do indices for strums

	var graphic = Paths.image("pixelUI/" + br);
	strum.loadGraphic(graphic, true, Math.floor(graphic.width / 4), Math.floor(graphic.height / 5));
	strum.animation.add('static', [strum.noteData]);
	strum.animation.add('pressed', [strum.noteData + 4, strum.noteData + 8], 12, false);
	strum.animation.add('confirm', [strum.noteData + 12, strum.noteData + 16], 16, false);
	strum.playAnim("static", true);
	strum.antialiasing = false;

	strum.scale.set(6, 6);    
	strum.updateHitbox();

	strum.defScale.x = strum.scale.x;
	strum.defScale.y = strum.scale.y;
	
	if(lastAnim != null)
	{
		strum.playAnim(lastAnim, true);
	}
}

function preSetupNoteSplash(){
	this.kill();
	return Function_Stop;
}